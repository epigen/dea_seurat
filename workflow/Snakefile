
##### libraries #####
import os
import sys
import pandas as pd
import yaml
from snakemake.utils import min_version

min_version("7.15.2")

##### module name #####
module_name = "dea_seurat"

##### setup report #####
report: os.path.join("report", "workflow.rst")

##### load config and sample annotation sheets #####
configfile: os.path.join("config", "config.yaml")

# load annotation
annot = pd.read_csv(config['annotation'], index_col='name')
analyses = list(annot.index)
# convert dictionary for parametrization of rules
annot_dict = annot.to_dict('index')

result_path = os.path.join(config["result_path"],'dea_seurat')

rule all:
    input:
        dea_results = expand(os.path.join(result_path,'{analysis}','DEA_results.csv'),
                            analysis = analyses,
                           ),
        dea_volcanos = expand(os.path.join(result_path,'{analysis}','plots','DEA_volcanos.png'),
                            analysis = analyses,
                           ),
        dea_all_stats = expand(os.path.join(result_path,'{analysis}','DEA_ALL_stats.csv'),
                               analysis = analyses,
                              ),
        dea_lfc_heatmap = expand(os.path.join(result_path,'{analysis}','plots','DEA_LFC_heatmap.png'),
                               analysis = analyses,
                              ),
        envs = expand(os.path.join(config["result_path"],'envs',module_name,'{env}.yaml'),env=['seurat','volcanos','ggplot','heatmap']),
        configs = os.path.join(config["result_path"],'configs',module_name,'{}_config.yaml'.format(config["project_name"])),
        annotations = os.path.join(config["result_path"],'configs',module_name,'{}_annot.csv'.format(config["project_name"])),
    resources:
        mem_mb=config.get("mem", "16000"),
    threads: config.get("threads", 1)
    log:
        os.path.join("logs","rules","all.log"),
    params:
        partition=config.get("partition"),

        
##### load rules #####
include: os.path.join("rules", "common.smk")
include: os.path.join("rules", "dea.smk")
include: os.path.join("rules", "visualize.smk")
include: os.path.join("rules", "envs_export.smk")
